plugins {
    id 'com.android.application'
    id 'com.google.gms.google-services'
}

android {
    namespace "com.aula.finansee"
    compileSdk 36

    defaultConfig {
        applicationId "com.aula.finansee"
        minSdk 24
        targetSdk 36
        versionCode 1
        versionName "1.0"

        // Lê as credenciais do arquivo local.properties (não versionado)
        def emailSender = ""
        def emailPassword = ""

        def localProps = new Properties()
        def localFile = rootProject.file("local.properties")

        if (localFile.canRead()) {
            localProps.load(localFile.newDataInputStream())
            emailSender = localProps.getProperty("EMAIL_SENDER", "")
            emailPassword = localProps.getProperty("EMAIL_APP_PASSWORD", "")
        }

        if (!emailSender || !emailPassword) {
            println("⚠️  Aviso: credenciais de e-mail não encontradas em local.properties.")
        }

        // Insere as credenciais no BuildConfig (acessíveis via BuildConfig.EMAIL_SENDER)
        buildConfigField "String", "EMAIL_SENDER", "\"${emailSender}\""
        buildConfigField "String", "EMAIL_APP_PASSWORD", "\"${emailPassword}\""

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    packagingOptions {
        resources {
            excludes += [
                    "META-INF/NOTICE.md",
                    "META-INF/LICENSE.md",
                    "META-INF/NOTICE.txt",
                    "META-INF/LICENSE.txt",
                    "META-INF/LICENSE",
                    "META-INF/NOTICE"
            ]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    dataBinding {
        enabled = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    buildFeatures {
        buildConfig true
        viewBinding true
    }
}

dependencies {
    implementation libs.appcompat
    implementation libs.material
    implementation libs.activity
    implementation libs.constraintlayout
    implementation libs.navigation.fragment
    implementation libs.navigation.ui

    testImplementation libs.junit
    androidTestImplementation libs.ext.junit
    androidTestImplementation libs.espresso.core

    // Módulo necessário para o uso do Fragment
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Módulos necessários para o Firebase
    implementation platform('com.google.firebase:firebase-bom:33.4.0')

    implementation 'com.google.firebase:firebase-auth'
    implementation 'com.google.firebase:firebase-database'
    implementation 'com.google.firebase:firebase-core:21.1.1'

    // Módulo necessário para os Sliders
    implementation ('com.heinrichreimersoftware:material-intro:2.0.0') {
        exclude group: 'com.android.support'
    }

    // Módulos necessários para o FloatingActionButton Speed Dial precisa
    implementation "com.leinardi.android:speed-dial:3.3.0"
    implementation 'com.google.android.material:material:1.12.0'

    // Módulo para calendário usado na PrincipalActivity
    implementation 'com.github.prolificinteractive:material-calendarview:2.0.1'

    // Módulo que traz o suporte ao Java 8 Date and Time API para Android
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.4'

    // Módulo para enviar email
    implementation 'com.sun.mail:android-mail:1.6.7'
    implementation 'com.sun.mail:android-activation:1.6.7'

    // Módulo que trás compat helpers para a API da splash screen
    implementation "androidx.core:core-splashscreen:1.0.1"
}

configurations.all {
    resolutionStrategy {
        force 'androidx.core:core:1.13.1'
    }
}